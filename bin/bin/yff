#!/usr/bin/perl -w

use strict;

use Getopt::Long;

my $a_ref;
my $a_exact;
my $a_dryRun;
my $a_help;

my $prettier = "./node_modules/.bin/prettier";

sub help() {
    print STDOUT <<'HELP';

yff - only format changed files

USAGE:
  >> yff

Options:
  --ref     (-r) Format files affected by Git ref
  --exact   (-e) Use with argument for exact file name match
  --dry-run (-d) Only show files that would be updated
  --help    (-h) See this message

EXAMPLES:
  >> yff
  >> yff Button
  >> yff Button.tsx --exact
  >> yff --ref HEAD~

HELP
}

sub getFirstNonOptionArg {
    my $arg = shift @ARGV;
    if (!defined $arg) {
        return;
    }
    return ($arg =~ /^-/) ? getFirstNonOptionArg() : $arg;
}

MAIN: {
    GetOptions(
        'ref=s' => \$a_ref,
        'exact' => \$a_exact,
        'dry-run' => \$a_dryRun,
        'help' => \$a_help
    ) || (help() && exit 1);

    my $firstArg=getFirstNonOptionArg();
    if (($firstArg && $firstArg eq "help") || defined $a_help) {
        help();
        exit 1;
    }

    if (!-e $prettier) {
        print "Error: prettier not found at ${prettier}\n";
        exit 1;
    }

    if ($firstArg) {
        if (defined $a_exact) {
            system("$prettier \"**/*$firstArg\" --write");
            exit 0;
        } else {
            my @files = `find . -iname "*$firstArg*" -not -path "./node_modules/*"`;
            foreach my $file (@files) {
                chomp $file;
                if (!-e $file) {
                    next;
                }
                if (!defined $a_dryRun) {
                    print "Formatting $file\n";
                    system("$prettier --write $file");
                } else {
                    print "$file\n";
                }
            }
        }
        exit 0;
    }

    my @changed_files;
    if (defined $a_ref) {
        @changed_files = `git diff-tree --no-commit-id --name-only ${a_ref} -r`;
    } else {
        @changed_files = `git diff --name-only`;
        # include untracked files as well
        push @changed_files, `git ls-files --others --exclude-standard`;
    }

    if (scalar @changed_files == 0) {
        print "No changed files\n";
        exit 0;
    }

    foreach my $file (@changed_files) {
        chomp $file;
        if (!-e $file) {
            next;
        }
        if (!defined $a_dryRun) {
            print "Formatting $file\n";
            system("$prettier --write $file");
        } else {
            print "$file\n";
        }
    }

    exit 0;
}

